apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.droibit.quickly"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        debug {
            storeFile rootProject.file('keystore/debug.keystore')
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dexOptions {
        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'asm-license.txt'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:preference-v14:$supportLibraryVersion"

    implementation "com.github.salomonbrys.kodein:kodein-android:$kodeinAndroidVersion"

    kapt "com.github.gfx.android.orma:orma-processor:$ormaVersion"
    implementation "com.github.gfx.android.orma:orma:$ormaVersion"

    implementation "io.reactivex:rxjava:$rxJavaVersion"
    implementation "io.reactivex:rxkotlin:$rxKotlinVersion"
    implementation "io.reactivex:rxandroid:$rxAndroidVersion"
    implementation "com.jakewharton.rxrelay:rxrelay:$rxRelayVersion"

    implementation "com.lapism:searchview:$searchViewVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.chibatching:kotpref:$kotprefVersion"
    implementation "com.github.droibit.chopsticks:chopstick:$chopsticksVersion"
    implementation "com.github.droibit.chopsticks:chopstick-preference:$chopsticksVersion"
    implementation "com.github.droibit.chopsticks:chopstick-resource:$chopsticksVersion"

    debugImplementation "com.facebook.stetho:stetho:$stethoVersion"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support:support-annotations:$supportLibraryVersion"

    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
}

apply plugin: 'com.cookpad.android.licensetools'